version = '0.0.1'
String libVersion = version

group = 'com.staticbloc.scissors'

apply plugin: 'com.github.dcendents.android-maven'

apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

ext {
  repositoryUsername = project.hasProperty('BINTRAY_USER') ? property('BINTRAY_USER') : ''
  repositoryKey = project.hasProperty('BINTRAY_KEY') ? property('BINTRAY_KEY') : ''
}

bintray {
  user = repositoryUsername
  key = repositoryKey
  configurations = ['archives']
  publish = true
  pkg {
    repo = 'scissors'
    name = POM_NAME
    userOrg = 'staticbloc'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/eygraber/scissors'
    publicDownloadNumbers = true
    version {
      name = libVersion
      released  = new Date()
      vcsTag = libVersion
    }
  }
}

artifactory {
  contextUrl = 'https://oss.jfrog.org/artifactory'
  publish {
    repository {
      repoKey = 'oss-snapshot-local'
      username = repositoryUsername
      password = repositoryKey
      maven = true
    }

    defaults {
      publishConfigs('archives')
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'com.staticbloc.scissors'
      artifactId POM_ARTIFACT_ID
      version libVersion
    }
  }
}

install {
  repositories.mavenInstaller {
    pom.groupId = 'com.staticbloc.scissors'
    pom.artifactId = POM_ARTIFACT_ID
    pom.version = libVersion

    pom.project {
      name POM_NAME
      packaging 'jar'
      description POM_DESCRIPTION
      url 'https://github.com/eygraber/scissors'

      scm {
        url 'https://github.com/eygraber/scissors'
        connection 'scm:git:https://github.com/eygraber/scissors.git'
        developerConnection 'scm:git:git@github.com:eygraber/scissors.git'
        tag 'HEAD'
      }

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
      }

      developers {
        developer {
          id 'eygraber'
          name 'Eliezer Graber'
        }
      }
    }
  }
}

task androidJavadocs(type: Javadoc) {
  source = android.sourceSets.main.java.source
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  failOnError false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
  classifier = 'javadoc'
  from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.source
}

artifacts {
  archives androidSourcesJar
  archives androidJavadocsJar
}